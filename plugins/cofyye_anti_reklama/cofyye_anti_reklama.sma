/* Plugin generated by AMXX-Studio */

#include <amxmodx>
#include <amxmisc>
#include <regex>
#include <colorchat>
#include <fakemeta>

#define PLUGIN "COFYYE Anti Reklama"
#define VERSION "1.0"
#define AUTHOR "cofyye"

#pragma semicolon 1

new pCvar_BlockChangeName;
new pCvar_Prefix[32]; 
new pCvar_BlockInChat;
new pCvar_BlockOnConnect;
new pCvar_ChangeNick;
new pCvar_NickName[128];
new pCvar_Logger;

//new const szPattern[] = "\b(?:[0-9]{1,3}\.){3}[0-9]{1,3}\b"; // pattern za normalan IP : 192.168.0.1
new const szPattern[] = "\b(?:\d\s*\d{0,2}|[1-9]\d{0,1}|1\d{0,2}|2[0-4]\d|25[0-5])\s*\.(?:\s*\d\s*\d\s*\d{0,2}|[1-9]\d{0,1}|1\d{0,2}|2[0-4]\d|25[0-5])\s*\.(?:\s*\d\s*\d{0,2}|[1-9]\d{0,1}|1\d{0,2}|2[0-4]\d|25[0-5])\s*\.(?:\s*\d\s*\d{0,2}|[1-9]\d{0,1}|1\d{0,2}|2[0-4]\d|25[0-5])\b"; // pattern za ip sa whitespace : 192  . 168.  0. 1
		

public plugin_init() {
	register_plugin(PLUGIN, VERSION, AUTHOR);
	
	register_clcmd("say", "OnClientSay");
	register_clcmd("say_team", "OnClientSay");
	
	register_forward(FM_ClientUserInfoChanged, "ClientUserInfoChanged");
	
	pCvar_BlockChangeName = register_cvar("cofyye_antireklama_blockchangename", "0", ADMIN_RCON);
	pCvar_BlockInChat = register_cvar("cofyye_antireklama_blockinchat", "1", ADMIN_RCON);
	pCvar_BlockOnConnect = register_cvar("cofyye_antireklama_blockonconnect", "1", ADMIN_RCON);
	pCvar_ChangeNick = register_cvar("cofyye_antireklama_changenick", "1", ADMIN_RCON);
	pCvar_Logger = register_cvar("cofyye_antireklama_logger", "1", ADMIN_RCON);
	
	register_cvar("cofyye_antireklama_prefix", "COFYYE_ANTIREKLAMA", ADMIN_RCON);
	register_cvar("cofyye_antireklama_nickname", "COFYYE ANTI REKLAMA", ADMIN_RCON);
}

public client_putinserver(id) {
	if(!is_user_bot(id) && get_pcvar_num(pCvar_BlockOnConnect) == 1) {
		new szName[128];
		new szSteamId[33];
		get_user_name(id, szName, charsmax(szName));
		get_user_authid(id, szSteamId, charsmax(szSteamId));
		
		new szErrCode;
		new szError[128];
		
		new szResponse = regex_match_simple(szName, szPattern, PCRE_MULTILINE, szError, charsmax(szError), szErrCode);
		
		if(szResponse > 0) {
			if(get_pcvar_num(pCvar_Logger) == 1) {
				Log("User %s is blocked on connect for IP address in their nick.", szName);
			}
			
			server_cmd("kick #%s %s", szSteamId, "You cannot have a IP address in nick");
		} else {
			return PLUGIN_CONTINUE;
		}
		
		return PLUGIN_HANDLED;
	}
	
	return PLUGIN_CONTINUE;
}


public ClientUserInfoChanged(id) {
	new szOldName[32], szNewName[32];
	pev(id, pev_netname, szOldName, charsmax(szOldName));
	
	if(!is_user_connected(id) || is_user_bot(id)) {
		return FMRES_IGNORED; 
	}
	
	if(szOldName[0]) {
		get_user_info(id, "name", szNewName, charsmax(szNewName));
	}
        
	if(!equal(szOldName, szNewName) && get_pcvar_num(pCvar_BlockChangeName) == 1) {
		set_user_info(id, "name", szOldName);
		
		get_cvar_string("cofyye_antireklama_prefix", pCvar_Prefix, charsmax(pCvar_Prefix));
		
		ColorChat(id, TEAM_COLOR, "^4[%s] ^1You cannot change nickname on server.", pCvar_Prefix);
		
		return FMRES_HANDLED;
	} else if(!equal(szOldName, szNewName)) {
		new szErrCode;
		new szError[128];
		
		new szResponse = regex_match_simple(szNewName, szPattern, PCRE_MULTILINE, szError, charsmax(szError), szErrCode);
		
		if(szResponse > 0 && get_pcvar_num(pCvar_ChangeNick) == 1) {
			get_cvar_string("cofyye_antireklama_nickname", pCvar_NickName, charsmax(pCvar_NickName));
			
			if(get_pcvar_num(pCvar_Logger) == 1) {
				Log("User %s have IP address in their nick so changed to %s", szOldName, pCvar_NickName);
			}
			
			set_user_info(id, "name", pCvar_NickName);
		} else if(szResponse > 0 && get_pcvar_num(pCvar_ChangeNick) == 0) {
			return FMRES_IGNORED;
		} else {
			return FMRES_HANDLED;
		}
	}
	
	return FMRES_IGNORED;
}

public OnClientSay(id) {
	if(get_pcvar_num(pCvar_BlockInChat) == 1) {
		new szMessage[256];
		
		read_args(szMessage, charsmax(szMessage));
		
		new szErrCode;
		new szError[128];
			
		new szResponse = regex_match_simple(szMessage, szPattern, PCRE_MULTILINE, szError, charsmax(szError), szErrCode);
		
		get_cvar_string("cofyye_antireklama_prefix", pCvar_Prefix, charsmax(pCvar_Prefix));
		
		if(szResponse > 0) {
			if(get_pcvar_num(pCvar_Logger) == 1) {
				new szName[128];
				get_user_name(id, szName, charsmax(szName));
				Log("User %s is blocked in chat because is wrote a IP address.", szName);
			}
			
			ColorChat(id, TEAM_COLOR, "^4[%s] ^1You cannot write IP address in chat.", pCvar_Prefix);
			
			return PLUGIN_HANDLED;
		} else {
			return PLUGIN_CONTINUE;
		}
	}
	
	return PLUGIN_CONTINUE;
}

public Log(const  msg[], any:...)
{
	static szMessage[256];
	vformat(szMessage, charsmax(szMessage), msg, 2);
	
	static iFilename[96];
	static iDir[64];
	if( !iDir[0] )
	{
		get_basedir(iDir, charsmax(iDir));
		add(iDir, charsmax(iDir), "/logs");
	}
	
	format(iFilename, charsmax(iFilename), "%s/cofyye_antireklama.log", iDir);
	
	log_to_file(iFilename, "%s", szMessage);
}
